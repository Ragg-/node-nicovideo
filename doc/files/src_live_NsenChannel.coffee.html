<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>src/live/NsenChannel.coffee</title>
    <link rel="stylesheet" href="http://yui.yahooapis.com/3.9.1/build/cssgrids/cssgrids-min.css">
    <link rel="stylesheet" href="../assets/vendor/prettify/prettify-min.css">
    <link rel="stylesheet" href="../assets/css/main.css" id="site_styles">
    <link rel="icon" href="../assets/favicon.ico">
    <script src="http://yui.yahooapis.com/combo?3.9.1/build/yui/yui-min.js"></script>
</head>
<body class="yui3-skin-sam">

<div id="doc">
    <div id="hd" class="yui3-g header">
        <div class="yui3-u-3-4">
                <h1><img src="../assets/css/logo.png" title="" width="117" height="52"></h1>
        </div>
        <div class="yui3-u-1-4 version">
            <em>API Docs for: </em>
        </div>
    </div>
    <div id="bd" class="yui3-g">

        <div class="yui3-u-1-4">
            <div id="docs-sidebar" class="sidebar apidocs">
                <div id="api-list">
                    <h2 class="off-left">APIs</h2>
                    <div id="api-tabview" class="tabview">
                        <ul class="tabs">
                            <li><a href="#api-classes">Classes</a></li>
                            <li><a href="#api-modules">Modules</a></li>
                        </ul>
                
                        <div id="api-tabview-filter">
                            <input type="search" id="api-filter" placeholder="Type to filter APIs">
                        </div>
                
                        <div id="api-tabview-panel">
                            <ul id="api-classes" class="apis classes">
                                <li><a href="../classes/CommentProvider.html">CommentProvider</a></li>
                                <li><a href="../classes/MyListItem.html">MyListItem</a></li>
                                <li><a href="../classes/MyListMeta.html">MyListMeta</a></li>
                                <li><a href="../classes/NicoMyListApi.html">NicoMyListApi</a></li>
                                <li><a href="../classes/NicoVideoApi.html">NicoVideoApi</a></li>
                                <li><a href="../classes/NicoVideoInfo.html">NicoVideoInfo</a></li>
                                <li><a href="../classes/NsenChannel.html">NsenChannel</a></li>
                            </ul>
                
                
                            <ul id="api-modules" class="apis modules">
                            </ul>
                        </div>
                    </div>
                </div>
            </div>
        </div>
        <div class="yui3-u-3-4">
                <div id="api-options">
                    Show:
                    <label for="api-show-inherited">
                        <input type="checkbox" id="api-show-inherited" checked>
                        Inherited
                    </label>
            
                    <label for="api-show-protected">
                        <input type="checkbox" id="api-show-protected">
                        Protected
                    </label>
            
                    <label for="api-show-private">
                        <input type="checkbox" id="api-show-private">
                        Private
                    </label>
                    <label for="api-show-deprecated">
                        <input type="checkbox" id="api-show-deprecated">
                        Deprecated
                    </label>
            
                </div>
            
            <div class="apidocs">
                <div id="docs-main">
                    <div class="content">
<h1 class="file-heading">File: src/live/NsenChannel.coffee</h1>

<div class="file">
    <pre class="code prettyprint linenums">
###*
# @class NsenChannel
###

_ = require &quot;lodash&quot;
Emitter = require &quot;../Emitter&quot;
Cheerio = require &quot;cheerio&quot;
deepFreeze = require &quot;deep-freeze&quot;
Request = require &quot;request-promise&quot;
{sprintf} = require(&quot;sprintf&quot;)
{CompositeDisposable} = require &quot;event-kit&quot;
QueryString = require &quot;querystring&quot;

APIEndpoints = require &quot;../APIEndpoints&quot;
NicoException = require &quot;../NicoException&quot;
NicoLiveInfo = require &quot;./NicoLiveInfo&quot;
NsenChannels = require &quot;./NsenChannels&quot;

module.exports =
class NsenChannel extends Emitter

    ###*
    # Nsenリクエスト時のエラーコード
    # @const {Object.&lt;String, String&gt;
    ###
    @RequestError  : deepFreeze
        NO_LOGIN : &quot;not_login&quot;
        CLOSED : &quot;nsen_close&quot;
        REQUIRED_TAG : &quot;nsen_tag&quot;
        TOO_LONG : &quot;nsen_long&quot;
        REQUESTED : &quot;nsen_requested&quot;

    # &quot;ログインしていません。&quot;
    # &quot;現在リクエストを受け付けていません。&quot;
    # &quot;リクエストに必要なタグが登録されていません。&quot;
    # &quot;動画が長過ぎます。&quot;
    # &quot;リクエストされたばかりです。&quot;

    @Gage : deepFreeze
        BLUE : 0
        GREEN : 1
        YELLOW : 2
        ORANGE : 3
        RED : 4


    @Channels       : NsenChannels


    ###*
    # @return Promise
    ###
    @instanceFor : (live, session) -&gt;
        nsen = new NsenChannel(live, session)
        nsen._attachLive(live)
        Promise.resolve nsen



    ###*
    # @private
    # @property {NicoLiveInfo} _live
    ###
    _live           : null

    ###*
    # @private
    # @property {CommentProvider} _commentProvider
    ###
    _commentProvider : null

    ###*
    # @private
    # @property {NicoSession} _session
    ###
    _session         : null

    ###*
    # 再生中の動画情報
    # @private
    # @property {NicoLiveInfo} _playingMovie
    ###
    _playingMovie   : null

    ###*
    # 最後にリクエストした動画情報
    # @private
    # @property {NicoVideoInfo} _requestedMovie
    ###
    _requestedMovie : null

    ###*
    # 最後にスキップした動画のID。
    # 比較用なので動画IDだけ。
    # @private
    # @property {String} _lastSkippedMovieId
    ###
    _lastSkippedMovieId : null

    ###*
    # （午前４時遷移時の）移動先の配信のID
    # @property {String} _nextLiveId
    ###
    _nextLiveId     : null


    ###*
    # @param {NicoLiveInfo} liveInfo
    # @param {NicoSession} _session
    ###
    constructor     : (liveInfo, @_session) -&gt;
        if liveInfo not instanceof NicoLiveInfo
            throw new TypeError &quot;Passed object not instance of NicoLiveInfo.&quot;

        if liveInfo.isNsenLive() is false
            throw new TypeError &quot;This live is not Nsen live streaming.&quot;

        super

        Object.defineProperties @,
            id  :
                get : -&gt; @getChannelType()

        @onDidChangeMovie @_didChangeMovie
        @onWillClose @_willClose


    ###*
    # @return {Promise}
    ###
    _attachLive : (liveInfo) -&gt;
        @_channelSubscriptions?.dispose()
        @_live = null
        @_commentProvider = null

        @_channelSubscriptions = sub = new CompositeDisposable
        @_live = liveInfo

        sub.add liveInfo.onDidRefresh =&gt;
            @_didLiveInfoUpdated()

        liveInfo.commentProvider({connect: true})
        .then (provider) =&gt;
            @_commentProvider = provider

            # if provider.isFirstResponseProsessed is no
            #     sub.add provider.onDidProcessFirstResponse (comments) =&gt;
            #         comments.forEach (comment) =&gt;
            #             @_didCommentReceived comment, {ignoreVideoChanged: true}
            #
            #         sub.add provider.onDidReceiveComment (comment) =&gt;
            #             @_didCommentReceived(comment)
            #
            # else
            sub.add provider.onDidReceiveComment (comment) =&gt;
                @_didCommentReceived(comment)

            sub.add provider.onDidEndLive =&gt; @_onLiveClosed()

            @_didLiveInfoUpdated()
            @fetch()


    ###*
    # チャンネルの種類を取得します。
    # @return {String} &quot;vocaloid&quot;, &quot;toho&quot;など
    ###
    getChannelType  : -&gt;
        @_live.get(&quot;stream.nsenType&quot;)


    ###*
    # 現在接続中の放送のNicoLiveInfoオブジェクトを取得します。
    # @return {NicoLiveInfo}
    ###
    getLiveInfo     : -&gt;
        @_live


    ###*
    # 現在利用しているCommentProviderインスタンスを取得します。
    # @return {CommentProvider?}
    ###
    commentProvider : -&gt;
        @_commentProvider


    ###*
    # 現在再生中の動画情報を取得します。
    # @return {NicoVideoInfo?}
    ###
    getCurrentVideo     : -&gt;
        return @_playingMovie


    ###*
    # @return {NicoVideoInfo?}
    ###
    getRequestedMovie : -&gt;
        @_requestedMovie


    ###*
    # スキップリクエストを送信可能か確認します。
    # 基本的には、sendSkipイベント、skipAvailableイベントで
    # 状態の変更を確認するようにします。
    # @return {boolean
    ###
    isSkipRequestable   : -&gt;
        video = @getCurrentVideo()
        return (video isnt null) and (@_lastSkippedMovieId isnt video.id)


    ###*
    # @private
    # @param {String} command NicoLive command with &quot;/&quot; prefix
    # @param {Array.&lt;String&gt;} params command params
    ###
    _processLiveCommands : (command, params = [], options = {}) -&gt;
        switch command
            when &quot;/prepare&quot;
                @emit &quot;will-change-movie&quot;

            when &quot;/play&quot;
                break if options.ignoreVideoChanged

                [source, view, title] = params
                videoId = /smile:((?:sm|nm)[1-9][0-9]*)/.exec(source)

                if videoId?[1]?
                    @_didDetectMovieChange videoId[1]
                    # @emit &quot;did-change-movie&quot;, videoId[1]

            when &quot;/reset&quot;
                [nextLiveId] = params
                @_nextLiveId = nextLiveId
                @emit &quot;will-close&quot;, nextLiveId

            when &quot;/nspanel&quot;
                [operation, entity] = params
                @_processNspanelCommand(operation, entity)

            when &quot;/nsenrequest&quot;
                [state] = params # &quot;on&quot;, &quot;lot&quot;
                @emit &quot;did-receive-request-state&quot;, state


    ###*
    # Processing /nspanel command
    # @private
    # @param {String} op
    # @param {String} entity
    ###
    _processNspanelCommand : (op, entity) -&gt;
        return if op isnt &quot;show&quot;

        panelState = QueryString.parse(entity)

        switch true
            when panelState.goodClick?
                @emit &quot;did-receive-good&quot;
                return

            when panelState.mylistClick?
                @emit &quot;did-receive-add-mylist&quot;
                return

        if panelState.dj?
            @emit &quot;did-receive-tvchan-message&quot;, panelState.dj
            return

        @emit &quot;did-change-panel-state&quot;, {
            goodBtn     : panelState.goodBtn is &quot;1&quot;
            mylistBtn   : panelState.mylistBtn is &quot;1&quot;
            skipBtn     : panelState.skipBtn is &quot;1&quot;
            title       : panelState.title
            view        : panelState.view | 0
            comment     : panelState.comment | 0
            mylist      : panelState.mylist | 0
            uploadDate  : new Date(panelState.date)
            playlistLen : panelState.playlistLen | 0
            corner      : panelState.corner isnt &quot;0&quot;
            gage        : panelState.gage | 0
            tv          : panelState.tv | 0
        }

        return


    ###*
    # サーバー側の情報とインスタンスの情報を同期します。
    # @return {Promise}
    ###
    fetch               : -&gt;
        return unless @_live?
            Promise.reject new NicoException
                message: &quot;LiveInfo not attached.&quot;

        # リクエストした動画の情報を取得
        liveId = @_live.get(&quot;stream&quot;).liveId

        @_live.fetch()

        .then =&gt;
            APIEndpoints.nsen.syncRequest(@_session, {liveId})

        .catch (e) =&gt;
            Promise.reject new NicoException
                message     : &quot;Failed to fetch Nsen request status. (#{e.message})&quot;
                previous    : e

        .then (res) =&gt;
            $res = Cheerio.load(res.body)(&quot;:root&quot;)

            status = $res.attr(&quot;status&quot;)
            errorCode = $res.find(&quot;error code&quot;).text()

            return if status isnt &quot;ok&quot; and errorCode isnt &quot;unknown&quot;
                Promise.reject new NicoException
                    message     : &quot;Failed to fetch Nsen request status. (#{errorCode})&quot;
                    code        : errorCode
                    response    : res.body

            return if errorCode is &quot;unknown&quot; and @_requestedMovie?
                @_requestedMovie = null
                @emit &quot;did-cancel-request&quot;
                Promise.resolve()

            # リクエストの取得に成功したら動画情報を同期
            videoId = $res.find(&quot;id&quot;).text()

            # 直前にリクエストした動画と内容が異なれば
            # 新しい動画に更新
            if not @_requestedMovie? or @_requestedMovie.id isnt videoId
                @_session.video.getVideoInfo(videoId)

        .then (movie) =&gt;
            return unless movie?

            @_requestedMovie = movie
            @emit &quot;did-send-request&quot;, movie
            Promise.resolve()


    ###*
    # コメントサーバーに再接続します。
    ###
    reconnect : -&gt;
        @_commentProvider.reconnect()


    dispose         : -&gt;
        @emit &quot;will-dispose&quot;
        @_live = null
        @_commentProvider = null
        @_channelSubscriptions.dispose()
        super



    #
    # Nsen control methods
    #

    ###*
    # リクエストを送信します。
    # @param {NicoVideoInfo|String} movie リクエストする動画の動画IDかNicoVideoInfoオブジェクト
    # @return {Promise}
    ###
    pushRequest     : (movie) -&gt;
        promise = if typeof movie is &quot;string&quot;
            @_session.video.getVideoInfo(movie)
        else
            Promise.resolve(movie)

        promise.then (movie) =&gt;
            movieId = movie.id
            liveId = @_live.get(&quot;stream.liveId&quot;)

            Promise.all([APIEndpoints.nsen.request(@_session, {liveId, movieId}), movie])

        .then ([res, movie]) =&gt;
            $res = Cheerio.load(res.body)(&quot;:root&quot;)

            return unless $res.attr(&quot;status&quot;) is &quot;ok&quot;
                Promise.reject new NicoException
                    message     : &quot;Failed to push request&quot;
                    code        : $res.find(&quot;error code&quot;).text()
                    response    : res

            @_requestedMovie = movie
            @emit &quot;did-send-request&quot;, movie
            Promise.resolve()


    ###*
    # リクエストをキャンセルします
    # @return {Promise}
    #   キャンセルに成功すればresolveされます。
    #   (事前にリクエストが送信されていない場合もresolveされます。）
    #   リクエストに失敗した時、エラーメッセージつきでrejectされます。
    ###
    cancelRequest   : -&gt;
        if not @_requestedMovie
            return Promise.resolve()

        liveId  = @_live.get(&quot;stream&quot;).liveId

        APIEndpoints.nsen.cancelRequest(@_session, {liveId})
        .then (res) =&gt;
            $res = Cheerio.load(res.body)(&quot;:root&quot;)

            return if $res.attr(&quot;status&quot;) isnt &quot;ok&quot;
                Promise.reject new NicoException
                    message     : &quot;Failed to cancel request&quot;
                    code        : $res.find(&quot;error code&quot;).text()
                    response    : res.body

            @emit &quot;did-cancel-request&quot;, @_requestedMovie
            @_requestedMovie = null
            Promise.resolve()


    ###*
    # Goodを送信します。
    # @return {Promise}
    #   成功したらresolveされます。
    #   失敗した時、エラーメッセージつきでrejectされます。
    ###
    pushGood        : -&gt;
        liveId  = @_live.get(&quot;stream&quot;).liveId

        APIEndpoints.nsen.sendGood(@_session, {liveId})
        .then (res) =&gt;
            $res = Cheerio.load(res.body)(&quot;:root&quot;)

            return if $res.attr(&quot;status&quot;) isnt &quot;ok&quot;
                Promise.reject new NicoException
                    message     : &quot;Failed to push good&quot;
                    code        : $res.find(&quot;error code&quot;).text()
                    response    : res.body

            @emit &quot;did-push-good&quot;
            Promise.resolve()



    ###*
    # SkipRequestを送信します。
    # @return {Promise}
    #   成功したらresolveされます。
    #   失敗した時、エラーメッセージつきでrejectされます。
    ###
    pushSkip        : -&gt;
        liveId  = @_live.get(&quot;stream&quot;).liveId
        movieId = @getCurrentVideo()?.id?

        if ! @isSkipRequestable()
            return Promise.reject &quot;Skip request already sended.&quot;

        APIEndpoints.nsen.sendSkip(@_session, {liveId})
        .then (res) =&gt;
            $res = Cheerio.load(res.body).find(&quot;:root&quot;)

            # 通信に失敗
            return if $res.attr(&quot;status&quot;) isnt &quot;ok&quot;
                Promise.reject new NicoException
                    message     : &quot;Failed to push skip&quot;
                    code        : $res.find(&quot;error code&quot;).text()
                    response    : res.body

            @_lastSkippedMovieId = movieId
            @emit &quot;did-push-skip&quot;
            Promise.resolve()


    ###*
    # コメントを投稿します。
    # @param {String} msg 投稿するコメント
    # @param {String|Array.&lt;String&gt;} [command] コマンド(184, bigなど)
    # @param {Number} [timeoutMs]
    # @return {Promise} 投稿に成功すればresolveされ、
    #   失敗すればエラーメッセージとともにrejectされます。
    ###
    postComment : (msg, command = &quot;&quot;, timeoutMs = 3000) -&gt;
        @_commentProvider?.postComment(msg, command, timeoutMs)



    ###*
    # 次のチャンネル情報を受信していれば、その配信へ移動します。
    # @return {Promise}
    #   移動に成功すればresolveされ、それ以外の時にはrejectされます。
    ###
    moveToNextLive      : -&gt;
        return Promise.reject() unless @_nextLiveId?

        liveId = @_nextLiveId
        return unless liveId?
            Promise.reject new NicoException
                message : &quot;Next liveId is unknown.&quot;

        # 放送情報を取得
        @_session.live.getLiveInfo(liveId)
        .then (liveInfo) =&gt;
            @_attachLive(liveInfo)
            @emit &quot;did-change-stream&quot;, liveInfo
            @fetch()



    #
    # Event Listeners
    #

    ###*
    # コメントを受信した時のイベントリスナ。
    #
    # 制御コメントの中からNsen内イベントを通知するコメントを取得して
    # 関係するイベントを発火させます。
    # @param {LiveComment} comment
    ###
    _didCommentReceived     : (comment, options = {ignoreVideoChanged : false}) -&gt;
        if comment.isControlComment() or comment.isPostByDistributor()
            [command, params...] = comment.comment.split(&quot; &quot;)
            @_processLiveCommands command, params, options

        @emit &quot;did-receive-comment&quot;, comment
        return


    ###*
    # 配信情報が更新された時に実行される
    # 再生中の動画などのデータを取得する
    # @param {NicoLiveInfo} live
    ###
    _didLiveInfoUpdated      : -&gt;
        content = @_live.get(&quot;stream&quot;).contents[0]?.content
        videoId = content and content.match(/^smile:((?:sm|nm)[1-9][0-9]*)/)

        unless videoId?[1]?
           @_didDetectMovieChange null
           return

        if (not @_playingMovie?) or @_playingMovie.id isnt videoId
            @_didDetectMovieChange videoId[1]


    ###*
    # 再生中の動画の変更を検知した時に呼ばれるメソッド
    # @private
    # @param {String} videoId 次に再生される動画のID
    ###
    _didDetectMovieChange  : (videoId) -&gt;
        return if @_videoInfoFetcher?

        beforeVideo = @_playingMovie

        if videoId is null
            @emit &quot;did-change-movie&quot;, null, beforeVideo
            @_playingMovie = null
            return

        return if beforeVideo?.id is videoId

        @_videoInfoFetcher = @_session.video.getVideoInfo(videoId).then (video) =&gt;
            @_playingMovie = video
            @_videoInfoFetcher = null
            @emit &quot;did-change-movie&quot;, video, beforeVideo
            return

        return


    ###*
    # チャンネルの内部放送IDの変更を検知するリスナ
    # @param {String} nextLiveId
    ###
    _willClose     : (nextLiveId) -&gt;
        @_nextLiveId = nextLiveId


    ###*
    # 放送が終了した時のイベントリスナ
    ###
    _onLiveClosed   : -&gt;
        @emit &quot;ended&quot;

        # 放送情報を差し替え
        @moveToNextLive()


    ###*
    # 再生中の動画が変わった時のイベントリスナ
    ###
    _didChangeMovie     : -&gt;
        @_lastSkippedMovieId = null
        @emit &quot;did-available-skip&quot;




    #
    # Event Handlers
    #

    ###*
    # @event NsenChannel#did-receive-comment
    # @param {NicoLiveComment} comment
    ###
    onDidReceiveComment : (listener) -&gt;
        @on &quot;did-receive-comment&quot;, listener


    ###*
    # @event NsenChannel#did-receive-good
    ###
    onDidReceiveGood : (listener) -&gt;
        @on &quot;did-receive-good&quot;, listener


    ###*
    # @event NsenChannel#did-receive-add-mylist
    ###
    onDidReceiveAddMylist : (listener) -&gt;
        @on &quot;did-receive-add-mylist&quot;, listener


    ###*
    # @event NsenChannel#did-push-good
    ###
    onDidPushGood : (listener) -&gt;
        @on &quot;did-push-good&quot;, listener


    ###*
    # @event NsenChannel#did-push-skip
    ###
    onDidPushSkip : (listener) -&gt;
        @on &quot;did-push-skip&quot;, listener


    ###*
    # @event NsenChannel#did-send-request
    # @param {NicoVideoInfo} movie
    ###
    onDidSendRequest : (listener) -&gt;
        @on &quot;did-send-request&quot;, listener


    ###*
    # @event NsenChannel#did-cancel-request
    # @param {NicoVideoInfo}
    ###
    onDidCancelRequest : (listener) -&gt;
        @on &quot;did-cancel-request&quot;, listener


    ###*
    # @event NsenChannel#did-change-movie
    # @param {NicoVideoInfo} nextMovie
    # @param {NicoVideoInfo} beforeMovie
    ###
    onDidChangeMovie : (listener) -&gt;
        @on &quot;did-change-movie&quot;, listener


    ###*
    # @event NsenChannel#did-available-skip
    ###
    onDidAvailableSkip : (listener) -&gt;
        @on &quot;did-available-skip&quot;, listener


    ###*
    # @event NsenChannel#will-close
    # @param {String} nextLiveId
    ###
    onWillClose : (listener) -&gt;
        @on &quot;will-close&quot;, listener


    ###*
    # @event NsenChannel#did-receive-request-state
    # @param {String} newState
    ###
    onDidReceiveRequestState : (listener) -&gt;
        @on &quot;did-receive-request-state&quot;, listener

    ###*
    # @event NsenChannel#did-change-panel-state
    # @property {Boolean} goodBtn
    # @property {Boolean} mylistBtn
    # @property {Boolean} skipBtn
    # @property {String} title
    # @property {Number} view
    # @property {Number} comment
    # @property {Number} mylist
    # @property {Date} uploadDate
    # @property {Number} playlistLen
    # @property {Boolean} corner
    # @property {Number} gage
    # @property {Number} tv
    ###
    onDidChangePanelState : (listener) -&gt;
        @on &quot;did-change-panel-state&quot;, listener


    ###*
    # @event NsenChannel#did-receive-tvchan-message
    # @param {String} message
    ###
    onDidReceiveTvchanMessage : (listener) -&gt;
        @on &quot;did-receive-tvchan-message&quot;, listener

    ###*
    # @event NsenChannel#will-dispose
    ###
    onWillDispose : (listener) -&gt;
        @on &quot;will-dispose&quot;, listener

    </pre>
</div>
                    </div>
                </div>
            </div>
        </div>
    </div>
</div>
<script src="../assets/vendor/prettify/prettify-min.js"></script>
<script>prettyPrint();</script>
<script src="../assets/js/yui-prettify.js"></script>
<script src="../assets/../api.js"></script>
<script src="../assets/js/api-filter.js"></script>
<script src="../assets/js/api-list.js"></script>
<script src="../assets/js/api-search.js"></script>
<script src="../assets/js/apidocs.js"></script>
</body>
</html>
